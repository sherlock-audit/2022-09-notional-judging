0x52

medium

# StrategyUtils#_executeDynamicTradeExactIn returns incorrect amountBought if buyToken is wstETH and tradeUnwrapped is true

## Summary

Wrapping logic is flawed when buyToken = wstETH and tradeUnwrapped = true. The result is that the stETH received from the trade isn't wrapped and it returns the amount of stETH that was bought rather than the amount of wstETH that was bought. Any function that uses this will receive an incorrect value for amountBought which will propagate errors forward.

## Vulnerability Detail

[StrategyUtils.sol#L61-L90](https://github.com/sherlock-audit/2022-09-notional/blob/main/leveraged-vaults/contracts/vaults/balancer/internal/strategy/StrategyUtils.sol#L61-L90)

        if (params.tradeUnwrapped && buyToken == address(Deployments.WRAPPED_STETH)) {
            buyToken = Deployments.WRAPPED_STETH.stETH();
        }

        // Sell residual secondary balance
        Trade memory trade = Trade(
            params.tradeType,
            sellToken,
            buyToken,
            amount,
            0,
            block.timestamp, // deadline
            params.exchangeData
        );

        (amountSold, amountBought) = trade._executeTradeWithDynamicSlippage(
            params.dexId, tradingModule, params.oracleSlippagePercent
        );

        if (
            params.tradeUnwrapped && 
            buyToken == address(Deployments.WRAPPED_STETH) &&
            amountBought > 0
        ) {
            IERC20(buyToken).checkApprove(address(Deployments.WRAPPED_STETH), amountBought);
            uint256 wrappedAmount = Deployments.WRAPPED_STETH.balanceOf(address(this));
            /// @notice the amount returned by wrap is not always accurate for some reason
            Deployments.WRAPPED_STETH.wrap(amountBought);
            amountBought = Deployments.WRAPPED_STETH.balanceOf(address(this)) - wrappedAmount;
        }

When buyToken = wstETH and tradeUnwrapped = true L62 sets buyToken to the contract address of stETH. Then when the trade is executed, _executeTradeWithDynamicSlippage will return the amount of stETH that was purchased during the swap in L76. The issues arrises when it tries to check the buyToken in L82. The buyToken address has been changed to stETH so it will return false, skipping L85-89. This results in 2 issues:

1. Contracts calling this one expect to receive wstETH back from the trade but instead are receiving stETH, because it never wraps the stETH to wstETH.

2. The function returns the amount of stETH that was purchased because the lines that wrap the stETH and adjust amountBought never execute. The problem is that this return value is assumed to be the amount of wstETH, which is not the case.

## Impact

StrategyUtils#_executeDynamicTradeExactIn doesn't properly wrap the stETH and returns an incorrect value. If stETH is not a reward token then the token will become lost with no way to retrieve it.

## Code Snippet

[StrategyUtils.sol#L41-L91](https://github.com/sherlock-audit/2022-09-notional/blob/main/leveraged-vaults/contracts/vaults/balancer/internal/strategy/StrategyUtils.sol#L41-L91)

## Tool used

Manual Review

## Recommendation

Change L82 to check for stETH as buyToken rather than wstETH:

        if (
            params.tradeUnwrapped && 
    -       buyToken == address(Deployments.WRAPPED_STETH) && 
    +       buyToken == Deployments.WRAPPED_STETH.stETH() &&
            amountBought > 0
        ) {
